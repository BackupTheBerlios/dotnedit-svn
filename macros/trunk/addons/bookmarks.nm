# jf_favorites
# jf_edit_favorites


#==================================================================================
# create bookmark
define jf_favorites {
bms = $jf_path_to["/NEDIT_HOME/"] "bookmarks.txt"
file = $file_path $file_name
list = read_file(bms)
if ($read_status == 0 || list == "")
    {
    s = append_file(file "\n",bms)
    if (s == 0)
        {
        dialog("Bookmark couldn't be created!")
        }
    return
    }

if (search_string(list,file,0) == -1)
    {
    s = append_file(file "\n",bms)
    if (s == 0)
        {
        dialog("Bookmark couldn't be created!")
        }
    }
}
    
#==================================================================================
# Delete/Open Bookmark
define jf_edit_favorites {

errmsg = jf_require("utilities.nm")
if (errmsg != "") {
    dialog(errmsg)
    return
}

book_file = $jf_path_to["/NEDIT_HOME/"] "bookmarks.txt"

bfiles = read_file(book_file)
if (bfiles == "")
    {
    dialog("No bookmarks there, yet!")
    return
    }
temp = split(bfiles, "\n")
list = ""
for (i = 0; i < temp[]- 1; i++) {
    list = list mk_extract_filename(temp[i]) $sub_sep "(" mk_extract_pathname(temp[i]) ")\n"
}
list = substring(list, 0, length(list) - 1)
selected = list_dialog("Bookmarks:", list, "Open", "Delete", "Cancel", "   ")

if (selected != "") {
    part = split(selected, $sub_sep)
    file = substring(part[1], 1, length(part[1]) - 1) part[0]
    if ($list_dialog_button == 1) {
    open(file)
    }
    if ($list_dialog_button == 2) {
        f = search_string(bfiles, file, 0)
        write_file((substring(bfiles, 0, f) substring(bfiles, $search_end+1,\
        length(bfiles))),book_file)
        }
}
}
